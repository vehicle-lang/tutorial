# Chapter 1. Getting Started: the Vehicle's Language

In this chapter we will introduce some basic features of **Vehicle** as a programming language. We will use the famous _ACAS Xu verification challenge_,
first introduced in 2017 by Guy Katz et al. in _"Reluplex: An Efficient SMT Solver for Verifying -- Deep Neural Networks" (<https://arxiv.org/pdf/1702.01135.pdf>)_

## Standard Components of a Verification Problem

In the simplest verification scenario, we will need a neural network $N : R^m \rightarrow R^n$, and a property of the network we wish to verify. Often, though not always, such property can be formulated based on our understanding of the domain where the neural network is used.
ACAS Xu stands for _Airborne Collision Avoidance System for unmanned aircraft_. The objective is to analyse the airctaft's position and distance relative to other airctafts and give collision avoidance instructions.

In particular, the following measurements are of importance:

- $\rho$: feet **measuring the distance to intruder**,
- $\theta, \psi$: radians **measuring angle of intrusion**,
- $v_{own}, v_{vint}$: feet per second - **the speed of both aircrafts**,
- $\tau$: seconds - **time until loss of vertical separation**,
- $a_{prev}$: **previous advisory**

as the following picture illustrates:
![ACAS Xu](images/acas_xu.png)

$\theta$ and $\psi$ are measured counter clockwise, and are always in the range $[−\pi, \pi]$.

Based on this data the neural network is to issue one of the following instructions:

- Clear-of-Conflict (CoC),
- weak left,
- weak right,
- strong left,
- strong right.

Different neural networks are trained to analyise the relation of input and output variables, each individual 
neural network uses only five input variables.  
Given five selected input parameters, and the five instructions above, a neural network $N_{AX} : R^5 \rightarrow R^5$ is trained, given the previous historic data. The exact architecture of the neural network, or its training mode are not important for our argument, and so we will omit the details for now.

The original paper by Guy Katz lists ten properties, but for the sake of the illustration we will just consider the first of them:
_If the intruder is distant and is significantly slower than the ownship, the score of a COC advisory will always be below a certain fixed
threshold._

## Basic Building Blocks in Vehicle

### Types

Unlike many Neural Network verifiers, Vehicle is a typeful language, and each specification file starts with declaring the types.
In the ACAS Xu case, these are

``` vehicle
type InputVector = Vector Rat 5
type OutputVector = Vector Rat 5
```

-- the types of vectors of rational numbers that the network will be taking as inputs and giving as outputs;
and ofcourse the type of the network itself:

``` vehicle
@network
acasXu : InputVector -> OutputVector
```

The `Vector` type represents a mathematical vector, or in programming terms can be thought of as a fixed-length array. One potentially unusual aspect in Vehicle is that the size of the vector (i.e the number of items it contains) must be known statically at compile time. This allows Vehicle to check for the presence of out-of-bounds errors at compile time rather than run time.

The full type is therefore written as `Vector A n`, which represents the type of vectors with `n` elements of type `A`. For example, `Vector Rat 5` is a vector of length $5$ that contains rational numbers.

**Vehicle** in fact has a comprehensive support for programming with vectors, which we will see throughout this tutorial. But the interested reader may go ahead and check the documentation pages for vectors: <https://vehicle-lang.readthedocs.io/en/stable/language/vectors.html>

Networks are declared by adding a `@network` annotation to a function declaration, as shown above. Note that although no implementation for the network is provided directly in the specification, `acasXu` can still be used in the specification as any other declared function would be.
This follows the **Vehicle** philosophy that specifications should be independent of any particular network, and should be able to be used to train/test/verify a range of candidate networks implementations.

### Values

Types for values are automatically inferred by **Vehicle**. For example, we can declare the number $\pi$ and its type will be inferred as rational (note the minimalistic syntax required to do that in **Vehicle**):

``` vehicle
pi = 3.141592
```

### Working with Vectors

Often, some amount of input or output pre-processing is expected when defining a neural network. In our case, it is assumed that the `acasXu` neural network inputs and outputs are normalised, i.e. the network does not work directly with units like $m/s$. However, the specifications (and verification properties) we want to write should ideally concern the original units.

#### Problem space versus Input space

When we encounter similar problems later, we will say we encountered an instance of _problem space / input space mismatch_.
These occur because neural network models impose certain constraints on how a problem can be expressed.
In the example above, values may need to be normalised. If we were to reason on input vectors
directly, we would be writing specifications in terms of the _input space_ (i.e. referring to the neural network inputs directly).
However, when reasoning about properties of neural networks, one often needs to refer to the original problem.
In this case specifications will be written in terms of the _problem space_. 
Being able to reason about problem space (alongside the input space) is a feature that distinguishes **Vehicle** from 
majority of the mainstream neural network verifiers, such as e.g. Marabou, ERAN, or $\alpha\beta$-Crown. 
Let us see how this happens in practice.

#### Vector Normalisation

We start with introducing the full block of code that normalises vectors, and will explain significant features 
of Vehicle syntax featured in the code block afterwards. 

For clarity, we define a new type synonym for unnormalised input vectors which are in the problem space.

``` vehicle
type UnnormalisedInputVector = Vector Rat 5
```

Next we define the minimum and maximum values that each input can take. These correspond to the range of the inputs that the network is designed
to work over.

``` vehicle
minimumInputValues : UnnormalisedInputVector
minimumInputValues = [0,0,0,0,0]

maximumInputValues : UnnormalisedInputVector
maximumInputValues = [60261.0, 2*pi, 2*pi, 1100.0, 1200.0]
```

Note that above is the first instance of vector definition we encounter. The type-checker will ensure that all vectors written in this way are of the correct size (in this case, `5`). An alternative method to vector definition is to use the `foreach` constructor, which is used to provide a value for each `index i`. This method is useful if the vector has some regular structure. In fact, the vector `minimumInputValues` could be defined in this way:

``` vehicle
minimumInputValues : UnnormalisedInputVector
minimumInputValues = foreach i . 0
```

Let us see how  `foreach` works with vector indexing. 
Having defined the range of minimim and maximum values, we can define a simple predicate saying whether a given input vector is in the right range:

``` vehicle
validInput : UnnormalisedInputVector -> Bool
validInput x = foreach i . minimumInputValues ! i <= x ! i <= maximumInputValues ! i
```

Then we define the mean values that will be used to scale the inputs:

``` vehicle
meanScalingValues : UnnormalisedInputVector
meanScalingValues = [19791.091, 0.0, 0.0, 650.0, 600.0]
```

We can now define the normalisation function that takes an input vector and
returns the unnormalised version.

``` vehicle
normalise : UnnormalisedInputVector -> InputVector
normalise x = foreach i .
  (x ! i - meanScalingValues ! i) / (maximumInputValues ! i)
```

Using this we can define a new function that first normalises the input
vector and then applies the neural network:

``` vehicle
normAcasXu : UnnormalisedInputVector -> OutputVector
normAcasXu x = acasXu (normalise x)
```

### Functions

In the above block, we saw function definitions for the first time, so let us highlight the important features of the **Vehicle** language concerning functions. 

#### Function declarations

Declarations may be used to define new functions. A declaration is of the form

``` vehicle
<name> : <type>
<name> [<args>] = <expr>
```

Observe how all functions above fit within this declaration scheme.

#### Function types

Functions make up the backbone of the **Vehicle** language.
The function type is written `A -> B` where `A` is the input type and `B` is the output type. For example the function `validInput` above takes values of the (defined) type of `UnnormalisedInputVector` and returns values of type `Bool`. The function `normalise` has the same input type, but its output type is `InputVector`, which was define as a vector of rational numbers of size $5$.

As is standard in functional languages, the function arrow associates to the right so `A -> B -> C` is therefore equivalent to `A -> (B -> C)`. 

#### Function application and composition

As in most functional languages, function application is written by juxtaposition of the function with its arguments. For example, given a function `f` of type `Rat -> Bool -> Rat` and arguments `x` of type `Rat` and `y` of type `Bool`, the application of `f` to `x` and `y` is written `f x y` and this expression has type `Bool`.
This is unlike imperative languages such as Python, C or Java where you would write `f(x,y)`.

Functions of suitable types can be composed.  For example, given a function `acasXu` of type `InputVector -> OutputVector`, a function `normalise` of type `UnnormalisedInputVector -> InputVector` and an argument `x` of type `UnnormalisedInputVector` the application of `acasXu` to the `InputVector` resulting from applying `normalise x` is written as `acasXu (normalise x)`, and this expression has type `OutputVector`.


#### Pre-defined functions

Some functions are pre-defined in **Vehicle**. For example, the above block uses multiplication `*`, division `/` and vector look up `!`.  

### Quantifiers

The final building block to discuss are *quantifiers*, in the block above these are `forall` and `foreach`. 
Let us start with `forall`. One of the main advantages of **Vehicle** is that it can be used to state and prove specifications that describe the network’s behaviour over an infinite set of values.
